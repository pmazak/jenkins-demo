import org.apache.commons.httpclient.HttpClient
import org.apache.commons.httpclient.methods.PostMethod
import org.apache.commons.httpclient.methods.StringRequestEntity

apply plugin: 'jetty'    
buildDir = "build/demo"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

repositories {
    mavenCentral()
    add(new org.apache.ivy.plugins.resolver.URLResolver()) {
        name = 'absoluteUrl'
        addArtifactPattern('http://[organization]/[artifact]')
    }
}
configurations {
    ci
}
dependencies {
    ci "mirrors.jenkins-ci.org/war/latest:jenkins.war"
    ci "hudson-ci.org/downloads/plugins/git/1.1.4:git.hpi"
    testCompile 'junit:junit:4.8.2'
}

def ciFiles = []
configurations.ci.each {
    ciFiles << it    
}

task jenkins << {
    project.buildDir = "build/8080"
    clean.execute()
    jettyRunWar {
        System.setProperty("JENKINS_HOME", JENKINS_HOME)
        webApp = ciFiles[0]
        contextPath = "/"
        doFirst {
            println JETTY_MESSAGE
        }
    }
    jettyRunWar.execute()
}

task createJob << {
    ant.copy(file: ciFiles[1], tofile: "$JENKINS_HOME/plugins/git.hpi", verbose: true)
    def jobName = "demo-CI"
    HttpClient httpClient = new HttpClient();
    def postMethod = new PostMethod("$LOCAL_URL/createItem?name=$jobName")
    def gitRepo = new File("").toURI().toString().replace("file:/", "")
    def config = new File("config/DemoCI.JobConfig.txt").text
    config = config.replace("[[REPLACEME]]", gitRepo)
    postMethod.requestEntity = new StringRequestEntity(config, "application/xml", "UTF-8")
    def responseCode = httpClient.executeMethod(postMethod)
    println "Creating $jobName job ... [$responseCode]"
}

task commitabunch << {
    def rand = new Random()
    def strs = RANDOM_COMMENT_WORDS.split(' ')
    def randCommits = rand.nextInt(21)+1
    def commitFile = "config/.commitabunch"
    new File(commitFile).delete()
    (0..randCommits).each {
       def randNum = rand.nextInt(101)+1
       def randStr = ""
       (0..10).each {
           randStr += " " + strs[rand.nextInt(strs.length)]
       }
       def comment = "QC-$randNum:$randStr"
       new File(commitFile) << "// $comment\n"
       ant.exec(command:"git commit -am '$comment'")
    }
}

JENKINS_HOME = "config/.jenkins"
LOCAL_URL = "http://localhost:8080"
JETTY_MESSAGE = """
Server is coming up...
    Browse to: $LOCAL_URL/
  (Control-C to stop)
"""
RANDOM_COMMENT_WORDS = "fixed a bug with the media content not wanting to shape the circle properly like the square was shaped working on unit test for class that seemed difficult"